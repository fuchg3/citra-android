plugins {
    id "com.android.application"
}

def buildType

def abiFilter = "arm64-v8a" //, "x86"

android {
    buildToolsVersion "30.0.3"

    compileSdkVersion 29

    lintOptions {
        // This is important as it will run lint but not abort on error
        // Lint has some overly obnoxious "errors" that should really be warnings
        abortOnError false

        //Uncomment disable lines for test builds...
        //disable 'MissingTranslation'bin
        //disable 'ExtraTranslation'
    }

    defaultConfig {
        // TODO If this is ever modified, change application_id in strings.xml
        applicationId "org.citra.citra_emu_v"
        minSdkVersion 26
        //noinspection OldTargetApi
        targetSdkVersion 29
        versionCode 1
        versionName "0.1"
        ndk.abiFilters abiFilter
    }

    signingConfigs {
        //release {
        //    storeFile file('')
        //    storePassword System.getenv('ANDROID_KEYPASS')
        //    keyAlias = 'key0'
        //    keyPassword System.getenv('ANDROID_KEYPASS')
        //}
    }

    applicationVariants.all { variant ->
        buildType = variant.buildType.name // sets the current build type
    }

    // Define build types, which are orthogonal to product flavors.
    buildTypes {

        // Signed by release key, allowing for upload to Play Store.
        release {
            signingConfig signingConfigs.debug
        }

        // builds a release build that doesn't need signing
        // Attaches 'debug' suffix to version and package name, allowing installation alongside the release build.
        relWithDebInfo {
            initWith release
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
            signingConfig signingConfigs.debug
            minifyEnabled false
            testCoverageEnabled false
            debuggable true
            jniDebuggable true
        }

        // Signed by debug key disallowing distribution on Play Store.
        // Attaches 'debug' suffix to version and package name, allowing installation alongside the release build.
        debug {
            // TODO If this is ever modified, change application_id in debug/strings.xml
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
            debuggable true
            jniDebuggable true
        }
    }

    externalNativeBuild {
        cmake {
            version '3.18.1'
            path "../../../CMakeLists.txt"
        }
    }

    ndkVersion '22.1.7171670'

    defaultConfig {
        externalNativeBuild {
            cmake {
                arguments "-DENABLE_QT=0", // Don't use QT
                        "-DENABLE_SDL2=0", // Don't use SDL
                        "-DENABLE_WEB_SERVICE=0", // Don't use telemetry
                        "-DANDROID_ARM_NEON=true" // cryptopp requires Neon to work

                abiFilters abiFilter
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.4.0'

    // For loading huge screenshots from the disk.
    //noinspection GradleDependency
    implementation 'com.squareup.picasso:picasso:2.8'

    // Allows FRP-style asynchronous operations in Android.
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'com.nononsenseapps:filepicker:4.2.1'
    implementation 'org.ini4j:ini4j:0.5.4'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation 'com.android.billingclient:billing:4.0.0'
}
